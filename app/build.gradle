buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

Properties props = new Properties()
props.load(new FileInputStream(file(project.property("sleepsense.properties"))))

android {
    signingConfigs {
        release {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias "sleepsense"
            keyPassword props['keystore.password']
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "au.com.ahbeard.sleepsense"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 21
        versionName "1.0.6"
    }
    buildTypes {
        release {
            debuggable false

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
        main.java.srcDirs += 'src/main/kotlin'
    }
    productFlavors {
        playstore {
        }
        staging {
            applicationId 'au.com.ahbeard.sleepsense.staging'

        }
        instore {
            versionName "1.0.6"
            applicationId 'au.com.ahbeard.sleepsense.instore'
        }
    }

}

dependencies {
    def androidSupportVersion = "25.1.0"

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':beddit-mobile-android-sensor')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:support-v4:$androidSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'io.reactivex:rxjava:1.1.7'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.7.0'
    compile 'com.trello:rxlifecycle:0.7.0'
    compile 'com.trello:rxlifecycle-android:0.7.0'

    compile 'com.google.android.gms:play-services-base:10.0.1'
    compile 'com.google.firebase:firebase-core:10.0.0'
    compile 'io.lqd:liquid-android:2.+@aar'

    compile 'com.jakewharton.timber:timber:4.3.0'

    testCompile 'junit:junit:4.12'

}


apply plugin: 'com.google.gms.google-services'